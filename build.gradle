plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// Version strings used for CI
// Note the spaces before and after the equals sign
ext.majorVersion = 6
ext.minorVersion = 1
ext.minecraftVersion = "1.13"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()

    maven {
        name 'Spigot'
        url 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }

    maven {
        name 'Sonatype'
        url 'https://oss.sonatype.org/content/groups/public'
    }

    maven {
        name 'vault-repo'
        url 'http://nexus.hc.to/content/repositories/pub_releases'
    }

    maven {
        name 'nu-langchecker'
        url 'https://raw.githubusercontent.com/Librazy/NyaaUtilsLangChecker/maven-repo'
    }

    maven {
        name 'ess-repo'
        url 'http://repo.ess3.net/content/groups/essentials'
    }

    maven {
        name 'langutils-repo'
        url 'https://raw.github.com/MascusJeoraly/LanguageUtils/mvn-repo/'
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
}

dependencies {
    api "org.spigotmc:spigot-api:$minecraftVersion-R0.1-SNAPSHOT"
    api 'javax.persistence:javax.persistence-api:2.2'
    shadow 'javax.persistence:javax.persistence-api:2.2'
    annotationProcessor 'org.librazy:NyaaUtilsLangChecker:2.0-SNAPSHOT'
    annotationProcessor "org.spigotmc:spigot-api:$minecraftVersion-R0.1-SNAPSHOT"
    compileOnly 'org.librazy:NyaaUtilsLangChecker:2.0-SNAPSHOT'
    compileOnly('net.milkbowl.vault:VaultAPI:1.6') {
        transitive = false
    }
    compileOnly('net.ess3:Essentials:2.13-SNAPSHOT') {
        transitive = false
    }
    compileOnly('com.meowj:LangUtils:1.9') {
        transitive = false
    }
    testImplementation 'org.mockito:mockito-core:2.18.3'
    testImplementation 'org.xerial:sqlite-jdbc:3.21.0.1'
}

compileJava {
    options.compilerArgs += ["-Xplugin:NyaaUtilsLangAnnotationProcessor", "-Xlint:deprecation,unchecked,rawtypes"]
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "cat.nyaa"
            artifactId "nyaacore"
            version "$majorVersion.$minorVersion-SNAPSHOT"
            from components.java
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

javadoc {
    (options as StandardJavadocDocletOptions).with {
        links 'https://docs.oracle.com/javase/8/docs/api/'
        links 'https://hub.spigotmc.org/javadocs/spigot/'
        links 'https://google.github.io/guava/releases/21.0/api/docs/'
        links 'https://ci.md-5.net/job/BungeeCord/ws/chat/target/apidocs/'

        locale 'en_US'
        encoding 'UTF-8'
        docEncoding 'UTF-8'
        addBooleanOption('keywords', true)

        def currentJavaVersion = JavaVersion.current()
        if (currentJavaVersion > JavaVersion.VERSION_1_9) {
            options.addBooleanOption('html5', true)
        }

        windowTitle = "NyaaCore Javadoc"
        String fullVersion = System.getenv("FULL_VERSION_STRING");
        if (fullVersion != null){
            docTitle = "<b>NyaaCore</b> " + System.getenv("FULL_VERSION_STRING")
        } else {
            docTitle = "<b>NyaaCore</b>"
        }
    }
}